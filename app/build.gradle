apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.maxisma.allaboutsamsung"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 223
        versionName "5.0-Beta-3"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Other configuration: fabric.properties, google-services.json, pathPrefix in manifest

        def youtubeKeyProperties = new Properties()
        file("youtube-key.properties").withInputStream { youtubeKeyProperties.load(it) }
        buildConfigField "String", "YOUTUBE_API_KEY", "\"${youtubeKeyProperties.youtubeKey}\""

        buildConfigField "String", "WEBVIEW_BASE_URL", "\"https://allaboutsamsung.de\""
        buildConfigField "String", "REST_BASE_URL", "\"https://allaboutsamsung.de/wp-json/wp/v2/\""
        buildConfigField "String", "APP_API_BASE_URL", "\"https://allaboutsamsung.de/wp-json/app-api/v2/\""
        buildConfigField "String", "COMMENTS_URL_TEMPLATE", "\"https://allaboutsamsung.de/?loadApiComments=1&guid=[POST_ID]\""
        buildConfigField "String", "LEGAL_NOTICE_URL", "\"https://allaboutsamsung.de/impressum\""
        buildConfigField "String", "YOUTUBE_PLAYLIST_ID", "\"UU-JTjqB5yT9C8kdIJRCW2nQ\""
        buildConfigField "String", "APPMOB_APP_ID", "\"ca-app-pub-5186136702634520~9544345137\""
        buildConfigField "String", "GOOGLE_ANALYTICS_ID", "\"UA-24939508-1\""
        resValue "string", "appmobPostListAdId", "ca-app-pub-5186136702634520/2021078335"
        buildConfigField "int", "BREAKING_CATEGORY_ID", "531"
        buildConfigField "int", "DEAL_TAG_ID", "342"

        manifestPlaceholders = [
                hostName: "allaboutsamsung.de"
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines "Enable"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'com.android.support:multidex:1.0.3'

    def supportVersion = "27.1.0"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    implementation "com.android.support:customtabs:$supportVersion"

    implementation 'androidx.core:core-ktx:0.2'

    def playServicesVersion = "11.8.0"
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"
    implementation "com.google.android.gms:play-services-ads:$playServicesVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    def moshiVersion = "1.5.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"

    def okHttpVersion = "3.9.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

    def retrofitVersion = "2.3.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:1.0.0"

    def coroutinesVersion = "0.22.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    def archVersion = "1.1.0"
    implementation "android.arch.lifecycle:extensions:$archVersion"
    kapt "android.arch.lifecycle:compiler:$archVersion"
    implementation "android.arch.persistence.room:runtime:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"

    implementation "org.jsoup:jsoup:1.11.2"

    def glideVersion = "4.4.0" // Up to 4.6.1 seems to be buggy
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    def daggerVersion = "2.14.1"
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.github.pwittchen:infinitescroll:0.0.1"

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    implementation 'com.evernote:android-job:1.2.2'
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"

    implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.9.0"
    implementation "com.android.support:exifinterface:$supportVersion"

    implementation 'nz.bradcampbell:paperparcel:2.0.5'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.5'
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.5'

    implementation 'com.github.takuji31.Koreference:koreference:2.0.0'

    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: "com.google.code.findbugs"
    }
    implementation('com.google.apis:google-api-services-youtube:v3-rev191-1.23.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: "com.google.code.findbugs"
    }

    implementation 'com.github.jkwiecien:EasyImage:1.3.1'
}

apply plugin: 'com.google.gms.google-services'