apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.maxisma.allaboutsamsung"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 228
        versionName "5.0.4"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Other configuration: fabric.properties, google-services.json, pathPrefix in manifest

        def youtubeKeyProperties = new Properties()
        file("youtube-key.properties").withInputStream { youtubeKeyProperties.load(it) }
        buildConfigField "String", "YOUTUBE_API_KEY", "\"${youtubeKeyProperties.youtubeKey}\""

        def appMobAppId = "ca-app-pub-5186136702634520~9544345137"
        buildConfigField "String", "WEBVIEW_BASE_URL", "\"https://allaboutsamsung.de\""
        buildConfigField "String", "REST_BASE_URL", "\"https://allaboutsamsung.de/wp-json/wp/v2/\""
        buildConfigField "String", "APP_API_BASE_URL", "\"https://allaboutsamsung.de/wp-json/app-api/v2/\""
        buildConfigField "String", "COMMENTS_URL_TEMPLATE", "\"https://allaboutsamsung.de/?loadApiComments=1&guid=[POST_ID]\""
        buildConfigField "String", "LEGAL_NOTICE_URL", "\"https://allaboutsamsung.de/impressum\""
        buildConfigField "String", "YOUTUBE_PLAYLIST_ID", "\"UU-JTjqB5yT9C8kdIJRCW2nQ\""
        buildConfigField "String", "APPMOB_APP_ID", "\"$appMobAppId\""
        buildConfigField "String", "GOOGLE_ANALYTICS_ID", "\"UA-24939508-1\""
        resValue "string", "appmobPostListAdId", "ca-app-pub-5186136702634520/2021078335"
        buildConfigField "int", "BREAKING_CATEGORY_ID", "531"
        buildConfigField "int", "DEAL_TAG_ID", "342"

        manifestPlaceholders = [
                hostName: "allaboutsamsung.de",
                appMobAppId: appMobAppId
        ]
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "androidx.preference:preference:1.0.0"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.browser:browser:1.0.0'

    implementation 'androidx.core:core-ktx:1.0.1'

    implementation "com.google.firebase:firebase-messaging:17.3.4"

    implementation "com.google.android.gms:play-services-ads:17.1.2"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    def moshiVersion = "1.8.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    def okHttpVersion = "3.12.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

    def retrofitVersion = "2.5.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    def coroutinesVersion = "1.1.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    def archVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$archVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$archVersion"
    implementation "androidx.room:room-runtime:$archVersion"
    kapt "androidx.room:room-compiler:$archVersion"

    implementation "org.jsoup:jsoup:1.11.3"

    def glideVersion = "4.8.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    def daggerVersion = "2.14.1"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "com.github.pwittchen:infinitescroll:0.0.2"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    implementation 'com.evernote:android-job:1.2.6'
    implementation "com.google.android.gms:play-services-gcm:16.0.0"

    implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.10.0"
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    implementation 'com.github.takuji31.Koreference:koreference:2.0.0'

    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: "com.google.code.findbugs"
    }
    implementation('com.google.apis:google-api-services-youtube:v3-rev206-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: "com.google.code.findbugs"
    }

    implementation 'com.github.jkwiecien:EasyImage:2.1.1'
}

apply plugin: 'com.google.gms.google-services'